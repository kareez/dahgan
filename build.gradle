buildscript {
    ext {
        kotlin_version = '1.3.11'
        junit_version = '4.12'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.11' apply false
}

allprojects {
    group = 'com.github.mhshams'
    version = '0.6.0'
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        classifier = 'javadoc'
    }

    publishing {
        publications {
            ossrh(MavenPublication) {
                from components.java
                artifact sourceJar
                artifact javadocJar

                pom {
                    name = 'Dahgan'
                    description = 'A YAML Reference Implementation'
                    url = 'https://github.com/kareez/dahgan'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                            comments = 'A business-friendly OSS license'
                        }
                    }
                    developers {
                        developer {
                            id = 'mhshams'
                            name = 'Mohammad Sarbandi'
                            email = 'm.h.shams@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git@github.com:kareez/dahgan.git'
                        developerConnection = 'scm:git:git@github.com:kareez/dahgan.git'
                        url = 'git@github.com:kareez/dahgan.git'
                    }
                }
                repositories {
                    maven {
                        url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                        credentials {
                            username sonatypeUsername
                            password sonatypePassword
                        }
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.ossrh
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

        testImplementation "junit:junit:$junit_version"
    }

    task copyTestResources(type: Copy) {
        from "$projectDir/src/test/resources"
        into "$buildDir/classes/kotlin/test"
    }

    processTestResources.dependsOn copyTestResources
}
